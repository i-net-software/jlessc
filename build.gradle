buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.github.gradle-nexus.publish-plugin:io.github.gradle-nexus.publish-plugin.gradle.plugin:+'
        classpath 'com.github.node-gradle.node:com.github.node-gradle.node.gradle.plugin:7.1.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:+'
        classpath 'io.github.gradle-nexus:publish-plugin:2.0.0' // Automates Sonatype Nexus publishing
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

repositories {
    mavenCentral()
}

// Determine Java version for conditional dependencies
def javaVersion = JavaVersion.current().majorVersion.toInteger()

dependencies {
    implementation		'com.google.code.findbugs:jsr305:3.0.2'
    implementation		'com.google.code.findbugs:annotations:3.0.1'
	testImplementation	'junit:junit:4.11'
    if( javaVersion >= 15 ) {
        testRuntimeOnly 'org.mozilla:rhino:1.7.+' // version 1.8.x required Java 11 which Gradle not accept with a smaller sourceCompatibility
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

test {
	testLogging {
        events "failed"
        exceptionFormat "short"
    }
    
	afterTest { desc, result ->
	       def color = 31;
	       switch(result){
	       		case "SUCCESS": color = 32; break;
	       		case "FAIL": color = 31; break;
	       }
	       
	       print "\033[0;37mExecuted \033[1m[${desc.className}]\t\033[1;${color}m${desc.name}\033[0;37m\twith result: "
	       println "\033[0;${color}m${result.resultType}\033[0m"
	}
}

/****************************************
 * Coverage
 ****************************************/
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
jacoco {
    toolVersion = '0.8.3'
}

jacocoTestReport {
    tasks.coveralls.dependsOn it
    reports {
        xml.required = true  // coveralls plugin depends on xml format report
        html.required = true
    }
}
 
if( false && System.getProperty( "java.vm.specification.version" ) > '1.8' ) { 
    check.dependsOn tasks.coveralls
}

/****************************************
 * Deploy to Maven
 ****************************************/
group = 'de.inetsoftware'
// Version is read from gradle.properties
version = project.findProperty('version') ?: '1.15-SNAPSHOT'

// Set archive base name (compatible with Gradle 8 and 9)
base.archivesName = 'jlessc'

jar {
    manifest {
        attributes( 'Implementation-Title': 'JLessC, a Less CSS compiler',
                    'Implementation-Vendor-Id': 'de.inetsoftware',
                    'Implementation-Vendor': 'i-net software GmbH, Berlin, Germany',
                    'Implementation-Version': version,
                    'Automatic-Module-Name': 'de.inetsoftware.jlessc',
                    'Bundle-SymbolicName': 'de.inetsoftware.jlessc',
                    'Export-Package': 'com.inet.lib.less',
                    'Bundle-Version': version)
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    
    withJavadocJar()
    withSourcesJar()
    
    // Configure encoding
    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
}

sourcesJar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

// Configure publishing
// - Snapshots: Published to Sonatype snapshot repository
// - Releases: Published to Sonatype staging repository (then closed and released)
// - Local development: Use `./gradlew publishToMavenLocal` to publish to mavenLocal()
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'jlessc'

            pom {
                name = 'JLessC'
                description = 'A Less CSS compiler written completely in Java (pure Java).'
                url = 'https://github.com/i-net-software/jlessc'
                
                scm {
                    connection = 'scm:git:git://github.com/i-net-software/jlessc.git'
                    developerConnection = 'scm:git:ssh://github.com/i-net-software/jlessc.git'
                    url = 'https://github.com/i-net-software/jlessc'
                }
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                
                developers {
                    developer {
                        id = 'vberlin'
                        name = 'Volker Berlin'
                        email = 'vberlin@inetsoftware.de'
                        organization = 'i-net software'
                        organizationUrl = 'https://www.inetsoftware.de/'
                    }
                }
            }
            
            // Remove all dependencies from POM (compile-time only, no runtime dependencies)
            pom.withXml {
                asNode().dependencies?.replaceNode {}
            }
        }
    }
    
    // For local development, use mavenLocal()
    repositories {
        mavenLocal()
    }
}

println "Publishing version: ${version}"
println "Note: For local development, use './gradlew publishToMavenLocal'"
println "      For snapshots, use './gradlew publishToSonatype' (requires Sonatype credentials)"
println "      Snapshots are available at: https://oss.sonatype.org/content/repositories/snapshots/"

// Configure Nexus Publish Plugin for Sonatype/Maven Central
// Configure for both snapshots and releases
// Snapshots go to: https://oss.sonatype.org/content/repositories/snapshots/
// Releases go to staging, then are closed and released to Maven Central
nexusPublishing {
    repositories {
        sonatype {
            // New Central Portal URLs
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            // Credentials - try both property name variations
            username.set(
                project.findProperty('ossrhUsername') 
                    ?: project.findProperty('sonatypeUsername')
                    ?: System.getenv('SONATYPE_USERNAME') 
                    ?: System.getenv('OSSRH_USERNAME')
                    ?: ""
            )
            password.set(
                project.findProperty('ossrhPassword')
                    ?: project.findProperty('sonatypePassword')
                    ?: System.getenv('SONATYPE_PASSWORD')
                    ?: System.getenv('OSSRH_PASSWORD')
                    ?: ""
            )
            // Staging profile ID - should be the UUID from Sonatype, not the domain
            // Get it from: https://central.sonatype.com/ -> Staging Profiles
            // Or leave it out to auto-detect by groupId
            // If you have a staging profile UUID, uncomment and set it:
            // stagingProfileId.set('xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx')
        }
    }
    
    // Configure closeStagingRepositories task (only for releases, not snapshots)
    closeStagingRepositories {
        closeStagingRepositories.dependsOn 'publishToSonatype'
        // Only close staging for releases (non-snapshot versions)
        onlyIf { !version.toString().endsWith('-SNAPSHOT') }
    }
    
    // For snapshots, just publish (don't close staging)
    // For releases, close and release staging repositories
    if (!version.toString().endsWith('-SNAPSHOT')) {
        publish.finalizedBy 'closeStagingRepositories'
    }
}

// Configure signing for Maven Central
// Sign both snapshots and releases when credentials are available
// Note: Signing is only required for Maven Central, not for mavenLocal()
def hasSigningKey = (System.getenv('SIGNING_KEY') != null || project.findProperty('signing.key') != null)
def isPublishingToMavenLocal = gradle.startParameter.taskNames.any { 
    it.contains('MavenLocal') || it.contains('mavenLocal')
}

if (!isPublishingToMavenLocal && hasSigningKey) {
    // Only sign when publishing to Sonatype and signing key is available
    signing {
        required { true }
        useInMemoryPgpKeys( 
            System.getenv('SIGNING_KEY') ?: project.findProperty('signing.key'), 
            System.getenv('SIGNING_PASSWORD') ?: project.findProperty('signing.password') 
        )
        sign publishing.publications.mavenJava
    }
} else {
    // Skip signing for mavenLocal
    signing {
        required { false }
    }
}

/****************************************
 * Benchmark of different less compilers.
 ****************************************/
apply plugin: 'com.github.node-gradle.node'

configurations {
    benchmarkLessCSS
    benchmarkLessEngine
}

dependencies {
    benchmarkLessCSS    'org.lesscss:lesscss:+'
    benchmarkLessEngine 'com.asual.lesscss:lesscss-engine:+'
}

// less test file
def lessFile = file("${sourceSets.test.output.resourcesDir}/com/inet/lib/less/samples/bootstrap/bootstrap-3.3.1.less")

/**
 * Bundle to call all single benchmark tests.
 */
task benchmark {
    dependsOn 'benchmarkJLessC'
    dependsOn 'benchmarkLessCSS'
    dependsOn 'benchmarkLessEngine'
    dependsOn 'benchmarkNodeLessCompile'
}

/**
 * Benchmark test for JLessC.
 */
task benchmarkJLessC( dependsOn: processTestResources ) {
    dependsOn 'jar'
    outputs.upToDateWhen { false }
    doFirst {
        // Use archiveFile for Gradle 8+ (archivePath was removed in Gradle 9)
        def jarFile = jar.archiveFile.get().asFile
        URL[] urls = [jarFile.toURI().toURL()]
        def classLoader = new URLClassLoader( urls );
        def compiler = Class.forName( "com.inet.lib.less.Less", true, classLoader ).newInstance();
        benchmarkTest( 'JLessC', {
            compiler."compile"( lessFile, true )
        } )
        classLoader.close()
    }
}

/**
 * Benchmark test for Lesscss.
 */
task benchmarkLessCSS( dependsOn: processTestResources ) {
    outputs.upToDateWhen { false }
    doFirst {
        def classLoader = getClassLoader( configurations.benchmarkLessCSS );
        def compiler = Class.forName( "org.lesscss.LessCompiler", true, classLoader ).newInstance();

        benchmarkTest( 'Lessccs', {
            compiler.compile( lessFile )
        } )
        classLoader.close()
    }
}


/**
 * Benchmark test for Lesscss.
 */
task benchmarkLessEngine( dependsOn: processTestResources ) {
    outputs.upToDateWhen { false }
    doFirst {
        def classLoader = getClassLoader( configurations.benchmarkLessEngine );
        def compiler = Class.forName( "com.asual.lesscss.LessEngine", true, classLoader ).newInstance();

        benchmarkTest( 'LessEngine', {
            compiler.compile( lessFile, true )
        } )
        classLoader.close()
    }
}

/**
 * NodeJS for compilation test with original less implementation
 */
task benchmarkNodeLessCompile( dependsOn: processTestResources ) {
    dependsOn 'npmInstall'
    outputs.upToDateWhen { false }
    doFirst {
        benchmarkTest( 'less.js', {
            exec {
                workingDir = lessFile.parentFile.absolutePath
                commandLine "${buildDir}/../node_modules/less/bin/lessc", lessFile.name, "${lessFile.parentFile}/output.css"
                ignoreExitValue = true
            }
        } );
    }
}

/**
 * Test routine
 * @param testName label for printing
 * @param callable call of the compiler
 */
void benchmarkTest( String testName, Closure callable ) {
    callable.call() // run once for the JIT
    def time = -System.currentTimeMillis()
    callable.call()
    time += System.currentTimeMillis()
    println( "\tResult for ${testName}:\033[1;33m\t${time} ms\033[0m" )
}

/**
 * Create a Java ClassLoader from a single configuration.
 * @param config the configuration
 * @return the ClassLoader
 */
ClassLoader getClassLoader( Configuration config ) {
    ArrayList urls = new ArrayList()
    config.files.each { File file ->
        // println '\t' + file.name
        urls += file.toURI().toURL()
    }
    return new URLClassLoader( urls.toArray(new URL[0]) );
}
